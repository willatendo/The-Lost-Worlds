buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		maven { url = 'https://repo.spongepowered.org/maven' }
		maven { url = 'https://plugins.gradle.org/m2/' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
	
plugins {
	id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.spongepowered.mixin'

version = '1.0.0'
group = 'lostworlds'
archivesBaseName = 'lostworlds'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
	shade
}

minecraft {
	mappings channel: 'official', version: '1.16.5'
	
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			arg "-mixin.config=lostworlds.mixins.json"

			mods {
				lostworlds {
					source sourceSets.main
				}
			}
		} 
		
		server {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			arg "-mixin.config=lostworlds.mixins.json"

			mods {
				lostworlds {
					source sourceSets.main
				}
			}
		}
		
		data {
			workingDirectory project.file('run')
			args '--mod', 'lostworlds', '--all', '--existing', file('src/main/resources').toString(), '--existing', file('src/generated/resources').toString(), '--output', file('src/generated/resources/')
					
			mods {
				lostworlds {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

mixin {
	add sourceSets.main, 'lostworlds.mixins.refmap.json'
	config 'lostworlds.mixins.json'
}

repositories {
	//Gecko
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	//JEI
	maven { url 'https://dvs1.progwml6.com/files/maven' }
	//TOP
	maven { url "https://maven.tterrag.com/" }
	//Tyrannotitan Lib
	maven { url "https://www.cursemaven.com" }
	//Craft Tweaker
	maven { url "https://maven.blamejared.com" }
	// Registrate
	maven { url "https://maven.tterrag.com/" }
	mavenLocal()
}	

dependencies {
	//Minecraft
	minecraft 'net.minecraftforge:forge:1.16.5-36.2.23'
	
	//Supported Mods
	compileOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.145:api")
	runtimeOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.145")
	
	compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.2.473")
	
	compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.6-8")
	
	//Library
	implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.16.5:3.0.65')
	
	implementation project(':1.16.5-Books')
	
	def registrate = "com.tterrag.registrate:Registrate:MC1.16.5-1.0.10"
	implementation fg.deobf(registrate)
	shade registrate
	
	//Mixins
	annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

shadowJar {
	classifier = ''
	configurations = [project.configurations.shade]
	relocate 'com.tterrag.registrate', 'lostworlds.repack.registrate'
	relocate 'tyrannotitanlib', 'lostworlds.repack.tyrannotitanlib'
}

jar {
	manifest {
		attributes([
			"Specification-Title": "lostworlds",
			"Specification-Vendor": "Willatendo",
			"Specification-Version": "1",
			"Implementation-Title": project.name,
			"Implementation-Version": "1.0.0",
			"Implementation-Vendor" :"Willatendo",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"MixinConfigs": "lostworlds.mixins.json"
		])
	}
}

reobf {
	shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar.finalizedBy('reobfJar')